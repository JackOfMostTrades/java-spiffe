
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    ext.protobufPluginVersion = '0.8.12'

    dependencies {
        classpath group: 'com.google.protobuf', name: 'protobuf-gradle-plugin', version: "${protobufPluginVersion}"
    }
}

ext {
    grpcVersion = '1.30.0'
    nettyVersion = '4.1.50.Final'
    protobufProtocVersion = '3.12.0'
    bouncyCastleVersion = '1.65'  //used for testing
}

apply plugin: 'com.google.protobuf'

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufProtocVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

dependencies {
    implementation group: 'io.grpc', name: 'grpc-netty', version: "${grpcVersion}"
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: "${grpcVersion}"
    implementation group: 'io.grpc', name: 'grpc-stub', version: "${grpcVersion}"
    testImplementation group: 'io.grpc', name: 'grpc-testing', version: "${grpcVersion}"

    implementation group: 'io.netty', name: 'netty-transport-native-epoll', version: "${nettyVersion}", classifier: 'linux-x86_64'
    implementation group: 'io.netty', name: 'netty-transport-native-kqueue', version: "${nettyVersion}", classifier: 'osx-x86_64'
    compileOnly group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    // library for processing JWT tokens and JOSE JWK bundles
    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '8.19'

    // using bouncy castle for generating x509 certs for testing purposes
    testImplementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.65'
}

