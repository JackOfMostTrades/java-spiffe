plugins {
    id 'com.github.kt3k.coveralls' version '2.10.1'
}

allprojects {
    repositories {
        mavenCentral()
    }
    apply plugin: 'jacoco'
}

subprojects {
    group = 'spiffe'
    version = '0.6.0'

    apply plugin: 'java-library'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    ext {
        jupiterVersion = '5.6.2'
        mockitoVersion = '3.3.3'
        lombokVersion = '1.18.12'
    }

    dependencies {
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
        implementation group: 'commons-validator', name: 'commons-validator', version: "1.6"

        testCompileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${jupiterVersion}"
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${jupiterVersion}"
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: "${jupiterVersion}"

        testCompileOnly group: 'org.mockito', name: 'mockito-core', version: "${mockitoVersion}"
        testRuntimeOnly group: 'org.mockito', name: 'mockito-junit-jupiter', version: "${mockitoVersion}"

        // Project Lombok dependency
        compileOnly group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
    }

    test {
        useJUnitPlatform()

        testLogging {
            afterSuite { desc, result ->
                if (!desc.parent) {
                    println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                }
            }
        }
    }
}

task jacocoTestReport(type: JacocoReport) {
    // Gather execution data from all subprojects
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    // Filter out autogenerated code
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/grpc/**', '**/exception/**', '**/provider/**'])
        }))
    }

    reports {
        xml.enabled true
        html.enabled true
    }
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
}

// always run the tests before generating the report
jacocoTestReport.dependsOn {
    subprojects*.test
    copyJars // workaround to prevent deleting the build folder before generating the reports
}

// copy submodules jars to a common folder for deploy
task copyJars(type: Copy) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from subprojects.collect { it.tasks.withType(Jar) }
    into "$buildDir/libs"
}

task assemble {
    dependsOn subprojects.assemble
}

assemble.finalizedBy copyJars

task clean {
    dependsOn subprojects.clean
    delete "$buildDir"
}

